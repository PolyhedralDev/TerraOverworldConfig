num randPrecision = 100;
num radius = 2.25 + randomInt(randPrecision) / randPrecision;
num warp = 1;
num warpFreq = 2;
num squish = 1 + randomInt(randPrecision) / randPrecision;
num radiusSquared = pow(radius, 2);
num yTranslate = -6 - randomInt(6);

for (num x = -radius - warp; x < radius + warp; x = x + 1) {
    for (num y = (-radius - warp) / squish; y < (radius + warp) / squish; y = y + 1) {
        for (num z = -radius - warp; z < radius + warp; z = z + 1) {
            num warpX = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 1000),
                warpFreq * (z + originZ()));
            num warpY = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 2000),
                warpFreq * (z + originZ()));
            num warpZ = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 3000),
                warpFreq * (z + originZ()));
            if (pow(x + warpX, 2) + pow((y + warpY) * squish, 2) + pow(z + warpZ, 2) < radiusSquared) {
                str setBlock = "minecraft:blackstone";
                if (randomInt(2) == 0) setBlock = "minecraft:magma_block";
                block(x, y + yTranslate, z, setBlock);
            }
        }
    }
}

for (num h = yTranslate + radius; h > yTranslate; h = h - 1) {
    if (getBlock(0, h, 0) == "minecraft:magma_block" ||
        getBlock(0, h, 0) == "minecraft:blackstone") {
        block(0, h - 1, 0, "minecraft:campfire[lit=true,signal_fire=true]", true);
        block(1, h - 1, 0, "minecraft:magma_block", true);
        block(0, h - 1, 1, "minecraft:magma_block", true);
        block(-1, h - 1, 0, "minecraft:magma_block", true);
        block(0, h - 1, -1, "minecraft:magma_block", true);
        block(0, h - 2, 0, "minecraft:magma_block", true);
    }
}



