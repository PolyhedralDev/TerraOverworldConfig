num randPrecision = 100;
num radius = 5 + randomInt(randPrecision) / randPrecision * 2;
num warp = 3;
num warpFreq = 8;
num squish = 0.8;
num radiusSquared = pow(radius, 2);
num yTranslate = -2;
str blockID = "minecraft:stone";

if (check(radius, -radius / squish, 0) != "LAND" ||
    check(-radius, -radius / squish, 0) != "LAND" ||
    check(0, -radius / squish, radius) != "LAND" ||
    check(0, -radius / squish, -radius) != "LAND"
) fail;

for (num x = -radius - warp; x < radius + warp; x = x + 1) {
    for (num y = (-radius - warp) / squish; y < (radius + warp) / squish; y = y + 1) {
        for (num z = -radius - warp; z < radius + warp; z = z + 1) {
            num warpX = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 1000),
                warpFreq * (z + originZ()));
            num warpY = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 2000),
                warpFreq * (z + originZ()));
            num warpZ = warp * sampler("simplex3",
                warpFreq * (x + originX()),
                warpFreq * (y + originY() + 3000),
                warpFreq * (z + originZ()));
            if (pow(x + warpX, 2) + pow((y + warpY) * squish, 2) + pow(z + warpZ, 2) < radiusSquared) {

                blockID = "minecraft:stone";
                if (randomInt(3) == 0) blockID = "minecraft:cobblestone";

                if (y > ((radius / squish) - 3) + (7 * (sampler("simplex3",
                    2 * (x + originX()),
                    4 * (y + originY() + 3000),
                    3 * (z + originZ()))))) {
                    blockID = "minecraft:pale_moss_block";
                }



                block(x, y + yTranslate, z, blockID);

            }
        }
    }
}