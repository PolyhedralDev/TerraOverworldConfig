num minLength = 1;
num maxLength = 7;
num variationFrequency = 10;

num length = round(minLength+(maxLength-minLength)*(sampler("simplex", (originX()+1000)*variationFrequency, originZ()*variationFrequency)+1)/2);
num clampedLength = length;
bool merge = false;
for (num i = 0; i <= length; i = i + 1) {
    str blockID = getBlock(0, i, 0);
    if (blockID != "minecraft:air") {
        if (blockID == "minecraft:pointed_dripstone") {
            merge = true;
            block(0, i+1, 0, "minecraft:pointed_dripstone[vertical_direction=down, thickness=tip_merge]");
        }
        clampedLength = i+1;
        break;
    }
}
for (num i = 1; i <= clampedLength; i = i + 1) {
    str blockID = "minecraft:pointed_dripstone[vertical_direction=up, ";
    if (i == 1) {
        if (merge) blockID = blockID + "thickness=tip_merge]";
        else blockID = blockID + "thickness=tip]";
    } else if (i == 2) {
        blockID = blockID + "thickness=frustum]";
    } else if (i == clampedLength) {
        blockID = blockID + "thickness=base]";
    }
    else {
        blockID = blockID + "thickness=middle]";
    }
    block(0, clampedLength-i, 0, blockID);
}