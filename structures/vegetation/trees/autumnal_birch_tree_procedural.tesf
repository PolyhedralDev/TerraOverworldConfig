// Free to modify variables
num leafRadius = 2.5 + randomInt(4);
num trunkHeight = 7 + randomInt(5);
num leafTopHeight = trunkHeight * 1.5;
num leafBottomHeight = 3 + randomInt(2);
str trunkBlockX = "minecraft:birch_log[axis=x]";
str trunkBlockY = "minecraft:birch_log[axis=y]";
str trunkBlockZ = "minecraft:birch_log[axis=z]";
str leafBlock   = "minecraft:oak_leaves[distance=1,persistent=false]";

// Tree gen logic
num leafMidPointRatio = 0.4;
num leafMidHeight = leafBottomHeight + (leafTopHeight-leafBottomHeight) * leafMidPointRatio;
for (num y = 0; y <= leafTopHeight; y = y + 1) {
    num radius = 0;

    // Radius formula - Two parabolas that intersect at the variables above
    if (y < leafMidHeight) {
        radius = (-pow((y-leafMidHeight)/(leafBottomHeight-leafMidHeight),2)+1)*leafRadius;
    } else {
        radius = (-pow((y-leafMidHeight)/(leafTopHeight-leafMidHeight),2)+1)*leafRadius;
    }

    if (y >= leafBottomHeight) {
        if (radius <= 1) { // Skip generating circle if radius is less than 1
            block(0, y, 0, leafBlock);
        } else {
            // Generate circle of leaves at y level
            num squareradius = radius * radius; // Square the radius to avoid unnecessary square root when comparing dist
            for (num x = round(-radius); x <= round(radius); x = x + 1) {
                for (num z = round(-radius); z <= round(radius); z = z + 1) {
                    num squaredist = x*x+z*z;
                    if (squaredist <= squareradius) {
                        if (squareradius - squaredist < squareradius * 0.5) { // If close to edge, make fuzzy
                            if (randomInt(2) == 0) {
                                block(x, y, z, leafBlock, false);
                            }
                        } else {
                            block(x, y, z, leafBlock, false);
                        }
                    }
                }
            }
        }
    }

    if (y <= trunkHeight) {
        block(0, y, 0, trunkBlockY); // Main trunk
    }
}
block(0, -1, 0, "minecraft:dirt");