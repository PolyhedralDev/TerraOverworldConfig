num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;
num twopi = 6.28318530718;

num trunkHeight = 4 + randomInt(4);
num trunkBase = 2 + randomInt(5);
str trunkBlock = "minecraft:mangrove_log";
str rootBlock = "minecraft:mangrove_roots";
str undergroundRootBlock = "minecraft:muddy_mangrove_roots";
str underwaterRootBlock = "minecraft:mangrove_roots[waterlogged=true]";

num sideBranchInterval = 3 + randomInt(2);
num sideBranchMinHeight = 2 + randomInt(3);
num sideBranchLengthMin = 3;
num sideBranchLengthVariation = 2;
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 1 + randomInt(randPrecision) / randPrecision / 2;
num sideBranchLeafRadiusMin = 2;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 2;
num sideBranchLeafBottomFlatness = 4;
num sideBranchLeafDensity = 0.6;
str sideBranchLeafStructure = "mangrove_leaves_clump";
str sideBranchBlockX = "minecraft:mangrove_log[axis=x]";
str sideBranchBlockY = "minecraft:mangrove_log[axis=y]";
str sideBranchBlockZ = "minecraft:mangrove_log[axis=z]";

num topBranches = 10 + randomInt(3);
num topBranchLengthUpwardsMin = 7 + randomInt(3);
num topBranchLengthOutwardsMin = 4 + randomInt(3);
num topBranchVerticalDirection = 0.4;
num topBranchLeafRadiusMin = 2;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 1.5;
num topBranchLeafBottomFlatness = 1;
num topBranchLeafDensity = 0.6;
str topBranchLeafStructure = "mangrove_leaves_clump";
str topBranchBlockX = "minecraft:mangrove_log[axis=x]";
str topBranchBlockY = "minecraft:mangrove_log[axis=y]";
str topBranchBlockZ = "minecraft:mangrove_log[axis=z]";

num sporeBlossomX = 0;
num sporeBlossomY = 0;
num sporeBlossomZ = 0;

// Generate roots at base of trunk
num roots = 3 + randomInt(2);
num rootMaxLength = 30;
num rootDroop = 0.06;
num rootVerticalDirection = -0.4 - randomInt(randPrecision) / randPrecision * 0.3;
num rootAngle = randomInt(randPrecision) / randPrecision * 2 * pi;
for (num r = 0; r < roots; r = r + 1) {
    // Branch direction vector
    num dx = sin(rootAngle);
    num dy = rootVerticalDirection;
    num dz = cos(rootAngle);
    // Normalize vector
    num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
    dx = dx / mag;
    dy = dy / mag;
    dz = dz / mag;

    for (num i = 0; i <= rootMaxLength; i = i + 0.5) {
        num cx = i * dx;
        num cy = trunkBase + i * dy;
        num cz = i * dz;

        // Place root block
        str currentBlock = getBlock(cx, cy, cz);
        if (currentBlock == "minecraft:stone") {
            break;
        } else if (currentBlock == "minecraft:mud" ||
            currentBlock == undergroundRootBlock
        ) {
                block(cx, cy, cz, undergroundRootBlock);
            } else if (getBlock(cx + 1, cy, cz) == "minecraft:water" ||
            getBlock(cx - 1, cy, cz) == "minecraft:water" ||
            getBlock(cx, cy, cz + 1) == "minecraft:water" ||
            getBlock(cx, cy, cz - 1) == "minecraft:water"
        ) {
                block(cx, cy, cz, underwaterRootBlock);
            } else {
            block(cx, cy, cz, rootBlock);
        }

        // Place moss carpet on root block
        num mossRoll = randomInt(10); // 1 in 10 chance
        if (mossRoll == 0) {
            block(cx, cy + 1, cz, "minecraft:moss_carpet", false);
        }

        // Point vector down more
        dy = dy - rootDroop;
        // Normalize vector
        mag = sqrt(pow2(dx) + pow2(dy) + pow2(dz));
        dx = dx / mag;
        dy = dy / mag;
        dz = dz / mag;
    }

    rootAngle = rootAngle + goldenAngle;
}

num branchAngle = randomInt(randPrecision) / randPrecision * 2 * pi;
for (num i = 0; i < trunkHeight; i = i + 1) {
    if (i > sideBranchMinHeight) {
        if (i % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision) / randPrecision * sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
            dx = dx / mag;
            dy = dy / mag;
            dz = dz / mag;
            // Branch origin
            num ox = 0;
            num oy = i + trunkBase;
            num oz = 0;
            // Set branch block based on largest unsigned vector component
            str branchBlock = sideBranchBlockY;
            if (abs(dx) > max(abs(dy), abs(dz))) branchBlock = sideBranchBlockX;
            else if (abs(dz) > max(abs(dy), abs(dx))) branchBlock = sideBranchBlockZ;
            num branchLength = sideBranchLengthMin + randomInt(randPrecision) / randPrecision * sideBranchLengthVariation;
            // Generate branch
            for (num l = 1; l <= branchLength; l = l + 1) {
                block(l * dx + ox, l * dy + oy, l * dz + oz, branchBlock);
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox + branchLength * dx;
            num loy = oy + branchLength * dy + sideBranchLeafTranslate;
            num loz = oz + branchLength * dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius / sideBranchLeafBottomFlatness); cy <= ceil(radius / sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx, 2) + pow(cy * squashFactor, 2) + pow(cz, 2);
                        if (distanceSquared < radiusSquared && randomInt(randPrecision) / randPrecision < sideBranchLeafDensity) {
                            structure(lox + cx, loy + cy, loz + cz, sideBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                        }
                    }
                }
            }
        }
    }
    block(0, i + trunkBase, 0, trunkBlock);
}

for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b / topBranches;
    num ti = 1 - t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx, 2) + pow(dy, 2) + pow(dz, 2));
    dx = dx / mag;
    dy = dy / mag;
    dz = dz / mag;
    // Branch origin
    num ox = 0;
    num oy = trunkBase + trunkHeight;
    num oz = 0;
    // Set branch block based on largest unsigned vector component
    str branchBlock = topBranchBlockY;
    if (abs(dx) > max(abs(dy), abs(dz))) branchBlock = topBranchBlockX;
    else if (abs(dz) > max(abs(dy), abs(dx))) branchBlock = topBranchBlockZ;
    num branchLength = topBranchLengthUpwardsMin * ti + topBranchLengthOutwardsMin * t;
    // Generate branch
    for (num l = 0; l <= branchLength; l = l + 1) {
        block(l * dx + ox, l * dy + oy, l * dz + oz, branchBlock);

        if (l == 1) {
            sporeBlossomX = l * dx + ox;
            sporeBlossomY = l * dy + oy - 1;
            sporeBlossomZ = l * dz + oz;
        }
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox + branchLength * dx;
    num loy = oy + branchLength * dy + topBranchLeafTranslate;
    num loz = oz + branchLength * dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius / topBranchLeafBottomFlatness); cy <= ceil(radius / topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx, 2) + pow(cy * squashFactor, 2) + pow(cz, 2);
                if (distanceSquared < radiusSquared && randomInt(randPrecision) / randPrecision < topBranchLeafDensity) {
                    structure(lox + cx, loy + cy, loz + cz, topBranchLeafStructure, "NONE", "CW_90", "CCW_90", "CW_180");
                }
            }
        }
    }
}

num sporeBlossomRoll = randomInt(50); // 1 in 50 chance
if (sporeBlossomRoll == 0) {
    structure(sporeBlossomX, sporeBlossomY, sporeBlossomZ, "spore_blossom", "NONE");
}

num beeNestRoll = randomInt(30); // 1 in 30 chance
if (beeNestRoll == 0) {
    structure(0, sideBranchMinHeight - 1, 0, "bee_nest", "NONE");
}

for (num v = 25; v > trunkBase + trunkHeight; v = v - 1) {
    num rx = sin(randomInt(randPrecision) / randPrecision * twopi) * (randomInt(5) + 5);
    num rz = cos(randomInt(randPrecision) / randPrecision * twopi) * (randomInt(5) + 5);
    structure(rx, v, rz, "place_vines", "NONE");
    structure(-rx, v, rz, "place_vines", "NONE");
    structure(rx, v, -rz, "place_vines", "NONE");
    structure(-rx, v, -rz, "place_vines", "NONE");
}