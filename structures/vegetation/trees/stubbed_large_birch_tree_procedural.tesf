num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;

num trunkHeight = 1+randomInt(5);
str trunkBlock = "minecraft:birch_log";

num sideBranchInterval = 3+randomInt(2);
num sideBranchMinHeight = 3+randomInt(3);
num sideBranchLengthMin = 1+randomInt(2);
num sideBranchLengthVariation = 2;
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 0.5+randomInt(randPrecision)/randPrecision/2;
num sideBranchLeafRadiusMin = 3;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 0.5;
num sideBranchLeafBottomFlatness = 1;
num sideBranchLeafRarity = 2;
str sideBranchLeafBlock = "minecraft:birch_leaves[distance=1,persistent=false]";
str sideBranchBlockX = "minecraft:birch_log[axis=x]";
str sideBranchBlockY = "minecraft:birch_log[axis=y]";
str sideBranchBlockZ = "minecraft:birch_log[axis=z]";

num topBranches = 3+randomInt(2);
num topBranchLengthUpwardsMin = 6+randomInt(20);
num topBranchLengthOutwardsMin = 4+randomInt(10);
num topBranchVerticalDirection = 2;
num topBranchLeafRadiusMin = 4;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 0.5;
num topBranchLeafBottomFlatness = 1;
num topBranchLeafRarity = 2;
str topBranchLeafBlock = "minecraft:birch_leaves[distance=1,persistent=false]";
str topBranchBlockX = "minecraft:birch_log[axis=x]";
str topBranchBlockY = "minecraft:birch_log[axis=y]";
str topBranchBlockZ = "minecraft:birch_log[axis=z]";

num branchAngle = randomInt(randPrecision)/randPrecision*2*pi;
for (num i = 0; i < trunkHeight; i = i + 1) {
    if (i > sideBranchMinHeight) {
        if (i % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision)/randPrecision*sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
            dx = dx/mag;
            dy = dy/mag;
            dz = dz/mag;
            // Branch origin
            num ox = 0;
            num oy = i;
            num oz = 0;
            // Set branch block based on largest unsigned vector component
            str branchBlock = sideBranchBlockY;
            if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = sideBranchBlockX;
            else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = sideBranchBlockZ;
            num branchLength = sideBranchLengthMin + randomInt(randPrecision)/randPrecision*sideBranchLengthVariation;
            // Generate branch
            for (num l = 0; l < branchLength; l = l + 1) {
                block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox+branchLength*dx;
            num loy = oy+branchLength*dy + sideBranchLeafTranslate;
            num loz = oz+branchLength*dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius/sideBranchLeafBottomFlatness); cy <= ceil(radius/sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                        if (distanceSquared/radiusSquared < randomInt(randPrecision)/randPrecision) {
                            if (randomInt(sideBranchLeafRarity) == 0) {
                                block(lox+cx, loy+cy, loz+cz, sideBranchLeafBlock, false);
                            }
                        }
                    }
                }
            }
        }
    }
    block(0, i, 0, trunkBlock);
}

for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b/topBranches;
    num ti = 1-t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;
    // Branch origin
    num ox = 0;
    num oy = trunkHeight;
    num oz = 0;
    // Set branch block based on largest unsigned vector component
    str branchBlock = topBranchBlockY;
    if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = topBranchBlockX;
    else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = topBranchBlockZ;
    num branchLength = topBranchLengthUpwardsMin*ti + topBranchLengthOutwardsMin*t;
    // Generate branch
    for (num l = 0; l < branchLength; l = l + 1) {
        block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox+branchLength*dx;
    num loy = oy+branchLength*dy + topBranchLeafTranslate;
    num loz = oz+branchLength*dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius/topBranchLeafBottomFlatness); cy <= ceil(radius/topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                if (distanceSquared/radiusSquared < randomInt(randPrecision)/randPrecision) {
                    if (randomInt(topBranchLeafRarity) == 0) {
                        block(lox+cx, loy+cy, loz+cz, topBranchLeafBlock, false);
                    }
                }
            }
        }
    }
}

block(0, -1, 0, "minecraft:dirt");