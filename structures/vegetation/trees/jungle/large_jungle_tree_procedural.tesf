num randPrecision = 100;
num goldenAngle = 2.39996;
num pi = 3.14159;
num twopi = 6.28318530718;

num trunkHeight = 12+randomInt(15);
str trunkBlock = "minecraft:jungle_log";
str rootBlock = "minecraft:jungle_wood";

num cocoaChance = 5;

num sideBranchInterval = 2+randomInt(2);
num sideBranchMinHeight = 1+randomInt(2);
num sideBranchLengthMin = 4+randomInt(2);
num sideBranchLengthVariation = 2;
num sideBranchVerticalVariation = 0;
num sideBranchVerticalDirection = 0.5+randomInt(randPrecision)/randPrecision/2;
num sideBranchLeafRadiusMin = 4;
num sideBranchLeafTranslate = 0;
num sideBranchLeafTopFlatness = 2;
num sideBranchLeafBottomFlatness = 3;
num sideBranchLeafDensity = 0.75;
str sideBranchLeafBlock = "minecraft:jungle_leaves[distance=1,persistent=false]";
str sideBranchBlockX = "minecraft:jungle_log[axis=x]";
str sideBranchBlockY = "minecraft:jungle_log[axis=y]";
str sideBranchBlockZ = "minecraft:jungle_log[axis=z]";

num topBranches = 8+randomInt(6);
num topBranchLengthUpwardsMin = 3+randomInt(4);
num topBranchLengthOutwardsMin = 6+randomInt(3);
num topBranchLengthVariation = 5;
num topBranchUpwardVariation = 5;
num topBranchVerticalDirection = 0.7;
num topBranchLeafRadiusMin = 4.1;
num topBranchLeafTranslate = 0;
num topBranchLeafTopFlatness = 2;
num topBranchLeafBottomFlatness = 3;
num topBranchLeafDensity = 0.75;
str topBranchLeafBlock = "minecraft:jungle_leaves[distance=1,persistent=false]";
str topBranchBlockX = "minecraft:jungle_log[axis=x]";
str topBranchBlockY = "minecraft:jungle_log[axis=y]";
str topBranchBlockZ = "minecraft:jungle_log[axis=z]";

// Generate roots at base of trunk
num roots = 3+randomInt(3);
num rootMaxDepth = 3;
num rootHeight = 5;
num rootLength = 2;
num rootAngle = randomInt(randPrecision)/randPrecision*2*pi;
for (num r = 0; r < roots; r = r + 1) {
    num dx = sin(rootAngle);
    num dz = cos(rootAngle);
    for (num l = 0; l <= rootLength; l = l + 1) {
        num height = rootHeight * (1-(l/rootLength));
        for (num h = height; h >= -rootMaxDepth; h = h - 1) {
            if (check(round(l*dx),round(h),round(l*dz)) != "AIR") break;
            block(l*dx, h, l*dz, rootBlock);
        }
    }
    rootAngle = rootAngle + goldenAngle;
}

num branchAngle = randomInt(randPrecision)/randPrecision*2*pi;
for (num i = 0; i < trunkHeight; i = i + 1) {
    if (i > sideBranchMinHeight) {
        if (i % sideBranchInterval == 0) {
            // Branch direction vector
            num dx = sin(branchAngle);
            num dy = sideBranchVerticalDirection + randomInt(randPrecision)/randPrecision*sideBranchVerticalVariation;
            num dz = cos(branchAngle);
            // Normalize vector
            num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
            dx = dx/mag;
            dy = dy/mag;
            dz = dz/mag;
            // Branch origin
            num ox = 0;
            num oy = i;
            num oz = 0;
            // Set branch block based on largest unsigned vector component
            str branchBlock = sideBranchBlockY;
            if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = sideBranchBlockX;
            else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = sideBranchBlockZ;
            num branchLength = sideBranchLengthMin + randomInt(randPrecision)/randPrecision*sideBranchLengthVariation;
            // Generate branch
            for (num l = 0; l <= branchLength; l = l + 1) {
                block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
            }
            // Rotate angle of next branch
            branchAngle = branchAngle + goldenAngle;
            // Generate leaf cluster at the end of the branch
            num radius = sideBranchLeafRadiusMin;
            num radiusSquared = pow(radius, 2);
            // Leaf cluster origin (end of branch)
            num lox = ox+branchLength*dx;
            num loy = oy+branchLength*dy + sideBranchLeafTranslate;
            num loz = oz+branchLength*dz;
            for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
                for (num cy = ceil(-radius/sideBranchLeafBottomFlatness); cy <= ceil(radius/sideBranchLeafTopFlatness); cy = cy + 1) {
                    for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                        num squashFactor = sideBranchLeafBottomFlatness;
                        if (cy > 0) squashFactor = sideBranchLeafTopFlatness;
                        num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                        if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < sideBranchLeafDensity) {
                            block(lox+cx, loy+cy, loz+cz, sideBranchLeafBlock, false);
                        }
                    }
                }
            }
        }
    }
    block(0, i, 0, trunkBlock);
    block(0, i, 1, trunkBlock);
    block(1, i, 0, trunkBlock);
    block(1, i, 1, trunkBlock);
}

for (num b = 1; b <= topBranches; b = b + 1) {
    num t = b/topBranches;
    num ti = 1-t;
    // Branch direction vector
    num dx = sin(branchAngle) * t;
    num dy = topBranchVerticalDirection;
    num dz = cos(branchAngle) * t;
    // Normalize vector
    num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
    dx = dx/mag;
    dy = dy/mag;
    dz = dz/mag;
    // Branch origin
    num ox = 0;
    num oy = trunkHeight;
    num oz = 0;
    // Set branch block based on largest unsigned vector component
    str branchBlock = topBranchBlockY;
    if (abs(dx) > max(abs(dy),abs(dz))) branchBlock = topBranchBlockX;
    else if (abs(dz) > max(abs(dy),abs(dx))) branchBlock = topBranchBlockZ;
    num branchLength = ((topBranchLengthUpwardsMin+randomInt(topBranchUpwardVariation))*ti) + (topBranchLengthOutwardsMin+randomInt(topBranchLengthVariation))*t;
    // Generate branch
    for (num l = 0; l <= branchLength; l = l + 1) {
        block(l*dx+ox, l*dy+oy, l*dz+oz, branchBlock);
    }
    // Rotate angle of next branch
    branchAngle = branchAngle + goldenAngle;
    // Generate leaf cluster at the end of the branch
    num radius = topBranchLeafRadiusMin;
    num radiusSquared = pow(radius, 2);
    // Leaf cluster origin (end of branch)
    num lox = ox+branchLength*dx;
    num loy = oy+branchLength*dy + topBranchLeafTranslate;
    num loz = oz+branchLength*dz;
    for (num cx = ceil(-radius); cx <= ceil(radius); cx = cx + 1) {
        for (num cy = ceil(-radius/topBranchLeafBottomFlatness); cy <= ceil(radius/topBranchLeafTopFlatness); cy = cy + 1) {
            for (num cz = ceil(-radius); cz <= ceil(radius); cz = cz + 1) {
                num squashFactor = topBranchLeafBottomFlatness;
                if (cy > 0) squashFactor = topBranchLeafTopFlatness;
                num distanceSquared = pow(cx,2)+pow(cy*squashFactor,2)+pow(cz,2);
                if (distanceSquared < radiusSquared && randomInt(randPrecision)/randPrecision < topBranchLeafDensity) {
                    block(lox+cx, loy+cy, loz+cz, topBranchLeafBlock, false);
                }
            }
        }
    }
}


for (num v = trunkHeight+topBranchLengthUpwardsMin; v > sideBranchMinHeight; v = v - 0.25) {
    num rx = sin(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(9) + 1);
    num rz = cos(randomInt(randPrecision)/randPrecision * twopi) * (randomInt(9) + 1);
    structure(rx, v, rz, "place_vines", "NONE");
    structure(-rx, v, rz, "place_vines", "NONE");
    structure(rx, v, -rz, "place_vines", "NONE");
    structure(-rx, v, -rz, "place_vines", "NONE");

    structure(rx, v, rz, "place_trunk_vines", "NONE");
    structure(-rx, v, rz, "place_trunk_vines", "NONE");
    structure(rx, v, -rz, "place_trunk_vines", "NONE");
    structure(-rx, v, -rz, "place_trunk_vines", "NONE");

    //cocoa
    num cocoaRoll = randomInt(cocoaChance)*10;
    if (cocoaRoll == 0) structure(rx, v, rz, "cocoa_bean", "NONE");

    cocoaRoll = randomInt(cocoaChance);
    if (cocoaRoll == 0) structure(-rx, v, rz, "cocoa_bean", "NONE");

    cocoaRoll = randomInt(cocoaChance);
    if (cocoaRoll == 0) structure(rx, v, -rz, "cocoa_bean", "NONE");

    cocoaRoll = randomInt(cocoaChance);
    if (cocoaRoll == 0) structure(-rx, v, -rz, "cocoa_bean", "NONE");
}

block(0, -1, 0, "minecraft:dirt");